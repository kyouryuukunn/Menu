//mainWindow.tjsの変更点はここにまとめた
kag.tagHandlers.s = function(elm)
{
	// 実行停止
	stablePosibility = true;
	//追加部分↓
	if (!sf.skipcontinue)
		cancelSkip(); //スキップをキャンセルするのをやめます。
	if (!sf.autocontinue)
		cancelAutoMode();
	//追加部分↑
	if(!usingExtraConductor) incRecordLabel(true);
	inSleep = true;
	if(recordHistoryOfStore == 2) // 2 : 選択肢 ( @s タグ ) ごと
	setToRecordHistory();
	notifyStable();
	return -1;
} incontextof kag;
kag.onCloseQuery = function ()
{
	saveSystemVariables();
	//追加部分↓
	//if(!askOnClose) { global.Window.onCloseQuery(true); return; }
	if(!askOnClose && !sf.exitAsk) { global.Window.onCloseQuery(true); return; }
	//追加部分↑
	global.Window.onCloseQuery(askYesNo("終了しますか？"));
} incontextof kag;
kag.onHelpAboutMenuItemClick = function (sender)
{
	// 「このソフトについて」ウィンドウを表示
	//追加部分↓
	tf.exitAsktemp=sf.exitAsk;
	sf.exitAsk=0;
	//追加部分↑
	var win = new global.KAGWindow(false, aboutWidth, aboutHeight);
	win.setPos(left + ((width - win.width)>>1), top + ((height - win.height)>>1));
	win.process('about.ks' ,,, true); // about.ks を immediate で表示
	win.showModal(); // モード付きで表示
	invalidate win;
	//追加部分↓
	sf.exitAsk=tf.exitAsktemp;
	//追加部分↑
} incontextof kag;
kag.goToStartWithAsk = function ()
{
	//追加部分↓
	// 最初に戻る(確認あり)
	//var result = askYesNo("最初に戻ります。よろしいですか ?");
	//if(result) goToStart();
	if (sf.titleAsk)
	{
		if (askYesNo("タイトルに戻りますか?")) goToStart();
	}
	else
	{
		goToStart();
	}
	//追加部分↑
} incontextof kag;
var restoreFromRightClick_org = kag.restoreFromRightClick;
kag.restoreFromRightClick = function ()
{
	restoreFromRightClick_org();
	if(autoMode) 
	{
		enterAutoMode(); 
		// inStable = 0でenterAutoMode()内でPrimaryClick()され 
		// ないので、ここでclickしておく 
		conductor.trigger('click'); 
	}
	// 右クリック中でskipMode設定された場合に対応
	if(skipMode_rclick)
	skipToNextStopByKey();
} incontextof kag;
// OUTFOCUSより
// ［システム‐未読もスキップ］メニューハンドラの定義
kag.onAllSkipClick = function(sender)
{
	sf.allskip = !sf.allskip;
	allSkipMenuItem.checked = sf.allskip;
	if (sf.allskip)
		skipToNextStopMenuItem.caption = "次の選択肢まで進む(&F)";
	else
		skipToNextStopMenuItem.caption = autoRecordPageShowing ? "次の選択肢/未読まで進む(&F)" : "次の選択肢まで進む(&F)";
} incontextof kag;
// ［未読もスキップ］メニューアイテムを作成
kag.allSkipMenuItem = new KAGMenuItem(kag, "未読もスキップ(&L)", 0, kag.onAllSkipClick, false);
kag.allSkipMenuItem.checked = sf.allskip;
// ［システム］メニューに［未読もスキップ］メニューアイテムを登録
kag.systemMenu.insert(kag.allSkipMenuItem, 3);
// kag.getCurrentRead()メソッドを差し換える
kag.getCurrentRead_org = kag.getCurrentRead;
kag.getCurrentRead = function()
{
	if (sf.allskip)                 // 未読もスキップするなら全て既読
		return true;
	else
		return getCurrentRead_org();  // そうでなければ旧kag.getCurrentRead()メソッドの戻り値を使う
} incontextof kag;
